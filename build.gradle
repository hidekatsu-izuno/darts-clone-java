apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'net.arnx'
archivesBaseName = 'darts-clone-java'
version = '0.6.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
	testCompile 'org.junit.jupiter:junit-jupiter-api:5.+'
	testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.+'
}

compileJava {
	options.encoding = 'UTF-8'
}

compileTestJava {
	options.encoding = 'UTF-8'
}

javadoc {
	options {
		links 'https://docs.oracle.com/javase/8/docs/api/'
		locale = 'en_US'
		encoding = 'UTF-8'
		docEncoding = 'UTF-8'
		charSet = 'UTF-8'
		author = true
	}
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: "${sonatypeUsername}", password: "${sonatypePassword}")
			}

			pom.project {
				name 'darts-clone-java'
				packaging 'jar'
				description 'Darts-Clone for Java'
				url 'https://github.com/hidekatsu-izuno/darts-clone-java'

				scm {
					url 'https://github.com/hidekatsu-izuno/darts-clone-java'
					connection 'scm:git:git://github.com/hidekatsu-izuno/darts-clone-java.git'
				}

				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'hidekatsu.izuno'
						name 'Hidekatsu Izuno'
						email 'hidekatsu.izuno@gmail.com'
					}
				}
			}
		}
	}
}
